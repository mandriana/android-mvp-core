buildscript {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    orgName = 'michaelandria'

    publishedGroupId = 'com.github.mandriana'
    libraryName = 'MVP base views'
    artifact = 'mvp-base-views-rx2'
    libraryVersion = '2.0.0'

    libraryDescription = 'A set of base views to be used with mvp-core'

    siteUrl = 'https://github.com/michaelandria/android-mvp-core'
    gitUrl = 'https://github.com/michaelandria/android-mvp-core.git'

    developerId = 'michaelandria'
    developerName = 'michaelandria'
    developerEmail = 'mika.andria@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

    bintrayLabels = ['aar', 'android', 'architecture', 'MVP']
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName project.ext.libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            consumerProguardFiles 'proguard-consumer.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        dagger {
            ext.flavorVersion = project.ext.libraryVersion
        }
        noDagger {
            ext.flavorVersion = project.ext.libraryVersion
        }
    }
    defineOutputForAars(libraryVariants)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Mvp library
    implementation project(':mvp')
    //implementation "com.github.mandriana:mvp-core-rx2:$rootProject.ext.mvp_core"

    // Android support
    api "androidx.appcompat:appcompat:$rootProject.ext.supportLibraryVersion"

    // Dagger 2
    daggerApi "com.google.dagger:dagger:$rootProject.ext.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.dagger"
    daggerCompileOnly 'javax.annotation:jsr250-api:1.0'

    testImplementation 'junit:junit:4.12'
}

def getAarFileNameForVariant(variant) {
    "${archivesBaseName}-${variant.flavorName}-${variant.productFlavors.get(0).flavorVersion}-${variant.buildType.name}.aar"
}

def defineOutputForAars(libraryVariants) {
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFileName != null && outputFileName.endsWith('.aar')) {
                def fileName = getAarFileNameForVariant(variant)
                outputFileName = new File(fileName)
            }
        }
    }
}

ext.flavorToBuild = "dagger"

apply from: rootProject.file('gradleScripts/bintray-flavors.gradle')
